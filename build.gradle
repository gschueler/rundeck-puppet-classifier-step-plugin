
plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'pl.allegro.tech.build.axion-release'

sourceCompatibility = 1.7
targetCompatibility = 1.7

defaultTasks 'clean', 'build'
ext.rundeckPluginVersion = '1.1'
ext.exportedClassnames="com.simplifyops.rundeck.plugin.puppet.ClassifierGroupsOptionGeneratorStep,com.simplifyops.rundeck.plugin.puppet.PinNodeToGroupStep"
scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}

project.version = scmVersion.version



configurations {
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile  {
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.6.4'

    pluginLibs 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    pluginLibs 'com.squareup.retrofit2:converter-jackson:2.0.0-beta4'

    testCompile group: 'junit', name:'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile "org.codehaus.groovy:groovy-all:2.3.7"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes(
                "Rundeck-Plugin-Version": rundeckPluginVersion,
                "Rundeck-Plugin-File-Version": version,
                "Rundeck-Plugin-Archive": "true",
                "Rundeck-Plugin-Classnames": exportedClassnames,
                "Rundeck-Plugin-Libs": "${libList}"
        )
    }
}

jar.dependsOn copyToLib

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
